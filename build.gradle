plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.springframework.boot' version '2.6.1'
    id "org.sonarqube" version "3.3"
    id "com.github.hierynomus.license" version "0.16.1"
}

apply plugin: 'io.spring.dependency-management'

task preBuild() {
    doLast {
        println 'Generating version number from git.'
        new File("build/resources/main/VERSION").withWriter { out ->
            out.println 'git rev-parse HEAD'.execute().text
        }
    }
}

build.dependsOn preBuild


sonarqube {
    properties {
        property "sonar.projectKey", "pivio_pivio-client"
    }
}

license {
    ignoreFailures true
    header = file('LICENSE')
}

jar {
    enabled = false
}

bootJar {
    archiveBaseName = 'pivio'
    archiveVersion = ''
    launchScript()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

springBoot {
    mainClass = "io.pivio.AppLauncher"
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter')
    implementation("org.springframework:spring-web")
    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.github.fge:json-schema-validator:2.2.6')
    implementation('com.fasterxml.jackson.module:jackson-module-jsonSchema')
    //TODO: Get rid of jackson-mapper-asl needed for deprecated class org.codehaus.jackson.schema.JsonSchema
    //TODO: and solely use jackson-module-jsonSchema. Also prevents mix of Jackson versions.
    implementation('org.codehaus.jackson:jackson-mapper-asl:1.9.13')
    implementation('org.tmatesoft.svnkit:svnkit:1.10.3')
    implementation('org.eclipse.jgit:org.eclipse.jgit:6.0.0.202111291000-r')
    implementation('commons-cli:commons-cli:1.5.0')
    implementation('org.jooq:joox:1.6.2')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.vintage:junit-vintage-engine')
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

tasks.named('wrapper') {
    gradleVersion = '7.3.1'
    distributionType = Wrapper.DistributionType.ALL
}
